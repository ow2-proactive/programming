apply plugin: 'build-dashboard'

allprojects {
    apply plugin: 'eclipse'
}

buildscript {
    repositories {
        if (project.hasProperty('local')) mavenLocal()
        mavenCentral()
        maven { url 'http://repo.activeeon.com/content/groups/proactive/' }
    }

    dependencies {
        classpath 'org.ow2.proactive:documentation:1.0.0-SNAPSHOT'
        classpath 'org.ow2.proactive:serialver-gradle:1.0-SNAPSHOT'
        classpath 'org.gradle.api.plugins:gradle-format-plugin:1.1'
    }
}

ext.serialver = programmingSerialver

subprojects {
    apply plugin: 'java'
    apply plugin: 'format'
    apply plugin: 'maven'

    format {
        configurationFile = file("$rootDir/compile/eclipse_formatter_config.xml")
    }
    build.dependsOn format

    group = 'org.objectweb.proactive'
    version = programmingVersion

    sourceCompatibility = 1.6
    compileJava.options.compilerArgs << '-Xlint:-options' // remove warning about bootstrap class path

    rootProject.buildscript.repositories.each {
        repositories.add(it)
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                snapshotRepository(url: "http://repo.activeeon.com/content/repositories/snapshots/") {
                    authentication(userName: "${System.getProperty('nexusUsername')}",
                            password: "${System.getProperty('nexusPassword')}")
                }
                repository(url: "http://repo.activeeon.com/content/repositories/releases/") {
                    authentication(userName: "${System.getProperty('nexusUsername')}",
                            password: "${System.getProperty('nexusPassword')}")
                }
            }
        }
    }

    dependencies {
        compile 'log4j:log4j:1.2.14'
        testCompile 'junit:junit:4.11'
    }

    task serialver(type: InsertSerialVersionUIDTask) {
        serialver = rootProject.serialver
    }

    jar {
        manifest {
            attributes("Implementation-Title": "ProActive",
                    "Implementation-Version": version,
                    "Implementation-Vendor": "Activeeon - OASIS - INRIA Sophia Antipolis",
                    "Implementation-URL": "http://proactive.inria.fr"
            )
        }
    }
}

ext.hasGcc = {
    try {
        def p = "gcc --version".execute(); p.waitFor(); p.exitValue() == 0
    } catch (e) {
        false
    }
}()

class StubTask extends JavaExec {
    @InputFiles
    def input = project.sourceSets.main.java.srcDirs
    @OutputFiles
    def output = project.fileTree(project.sourceSets.main.output.classesDir.parent)

    def StubTask() {
        project.configurations { stubClasspath }
        project.dependencies { stubClasspath project.project(':programming-build-utils') }
        project.jar.dependsOn this
        setMain('ant.AntStubGenerator$Main')
        setClasspath((project.configurations.stubClasspath + project.sourceSets.main.runtimeClasspath))
        setArgs([input.toArray()[0], project.sourceSets.main.output.classesDir])
        logging.captureStandardOutput LogLevel.INFO
    }

    def setClasses(List classes) {
        setArgs(getArgs() + classes)
    }
}

task clean << {
	delete "$rootDir/dist"
}

project(':programming-util') {
    dependencies {
        compile(
             project(':programming-annotation')
        )
    }
}
project(':programming-core') {


    dependencies {
        compile(
                'org.eclipse.jetty:jetty-server:8.1.15.v20140411',
		'org.eclipse.jetty:jetty-servlet:8.1.15.v20140411',
		'org.eclipse.jetty:jetty-xml:8.1.15.v20140411',

                'org.tmatesoft.svnkit:trilead-ssh2:build213-svnkit-1.3-patch',
                'commons-cli:commons-cli:1.1',
                'org.javassist:javassist:3.18.1-GA',
                'xerces:xercesImpl:2.8.1',

                project(':programming-annotation'),
                project(':programming-util')
        )
    }


    task stub(type: StubTask) {
        classes = ['org.objectweb.proactive.core.util.wrapper.BooleanMutableWrapper',
                'org.objectweb.proactive.core.util.wrapper.BooleanWrapper',
                'org.objectweb.proactive.core.util.wrapper.DoubleMutableWrapper',
                'org.objectweb.proactive.core.util.wrapper.DoubleWrapper',
                'org.objectweb.proactive.core.util.wrapper.FloatMutableWrapper',
                'org.objectweb.proactive.core.util.wrapper.FloatWrapper',
                'org.objectweb.proactive.core.util.wrapper.GenericTypeWrapper',
                'org.objectweb.proactive.core.util.wrapper.IntMutableWrapper',
                'org.objectweb.proactive.core.util.wrapper.IntWrapper',
                'org.objectweb.proactive.core.util.wrapper.LongMutableWrapper',
                'org.objectweb.proactive.core.util.wrapper.LongWrapper',
                'org.objectweb.proactive.core.util.wrapper.StringMutableWrapper',
                'org.objectweb.proactive.core.util.wrapper.StringWrapper',

                'org.objectweb.proactive.core.runtime.ProActiveRuntime',
                'org.objectweb.proactive.core.body.UniversalBody',
                'org.objectweb.proactive.core.jmx.util.JMXNotificationListener',
                'org.objectweb.proactive.core.util.log.remote.ProActiveLogCollector',
                'org.objectweb.proactive.core.jmx.ProActiveConnection',
                'org.objectweb.proactive.core.jmx.server.ProActiveServerImpl']
    }
    test {
        forkEvery 1
        systemProperties << ['proactive.classloading.useHTTP': false]
    }

    // to get rid of warning about sun internal api usage
    compileJava.options.compilerArgs << '-XDignore.symbol.file'
    compileJava.options.compilerArgs << '-Xlint:unchecked'
}

project(':programming-build-utils') {
    apply plugin: 'groovy'
    dependencies {
        compile gradleApi()
        compile 'ant:ant:1.7.0'
        compile 'commons-cli:commons-cli:1.2'
        compile "log4j:log4j:1.2.14"
        compile 'org.jvnet.winp:winp:1.14'
    }
}

project('programming-extensions') {
    subprojects {
        dependencies {
            compile(
                    project(':programming-core'),
                    project(':programming-annotation')
            )
        }
    }
}

project('programming-extensions:programming-extension-amqp') {
    dependencies {
        compile(
                'com.rabbitmq:amqp-client:3.0.2',
        )
    }
}

project('programming-extensions:programming-extension-annotation') {
    dependencies {
        compile files("${System.properties['java.home']}/../lib/tools.jar")
    }
}

def vfs2CompileDependencies = ['org.apache.commons:commons-vfs2:2.0']

def vfs2RuntimeDependencies = [
        'commons-net:commons-net:2.2',
        'commons-httpclient:commons-httpclient:3.1',
        'com.jcraft:jsch:0.1.51',
        'org.apache.jackrabbit:jackrabbit-webdav:1.5.2',
        'org.slf4j:slf4j-api:1.6.1']

project('programming-extensions:programming-extension-vfsprovider') {
    dependencies {
        compile(
                vfs2CompileDependencies,
                project(':programming-extensions:programming-extension-annotation')
        )
        testCompile 'org.apache.commons:commons-vfs2:2.0:tests'
        runtime vfs2RuntimeDependencies
    }
    test {
        systemProperties << ['proactive.classloading.useHTTP': false]
    }
    task stub(type: StubTask) {
        classes = ['org.objectweb.proactive.extensions.vfsprovider.protocol.FileSystemServer',
                'org.objectweb.proactive.extensions.vfsprovider.server.FileSystemServerImpl']
    }
}

project('programming-extensions:programming-extension-dataspaces') {
    dependencies {
        compile(
                vfs2CompileDependencies,
                project(':programming-extensions:programming-extension-vfsprovider')
        )
        testCompile(
                project(':programming-extensions:programming-extension-vfsprovider').sourceSets.test.output,
                project(':programming-extensions:programming-extension-pamr'),
                'commons-io:commons-io:2.4'
        )
        runtime vfs2RuntimeDependencies
    }
    test {
        forkEvery 1
        systemProperties << ['proactive.classloading.useHTTP': false]
    }
    task stub(type: StubTask) {
        classes = ['org.objectweb.proactive.extensions.dataspaces.core.naming.NamingService']
    }
}

project('programming-extensions:programming-extension-gcmdeployment') {
    dependencies {
        compile(
                'commons-cli:commons-cli:1.1',
                project(':programming-extensions:programming-extension-dataspaces')
        )
    }
    task stub(type: StubTask) {
        classes = ['org.objectweb.proactive.extensions.gcmdeployment.GCMApplication.GCMApplicationImpl',
                'org.objectweb.proactive.extensions.gcmdeployment.core.GCMVirtualNodeImpl']
    }
}

project('programming-extensions:programming-extension-pnp') {
    dependencies {
        compile(
                'io.netty:netty:3.3.0.Final'
        )
    }
}

project('programming-extensions:programming-extension-pnpssl') {
    dependencies {
        compile(
                'bouncycastle:bcprov-jdk15:136',
                'io.netty:netty:3.3.0.Final',

                project(':programming-extensions:programming-extension-pnp')
        )
    }
}

project('programming-extensions:programming-extension-processbuilder') {
    dependencies {
        compile(
                'net.java.dev.jna:jna:3.4.0',
                'net.java.dev.jna:platform:3.4.0'
        )
    }

    if (hasGcc) {
        def configureSuer = { task, arch ->
            buildDir.mkdirs()
            task.ignoreExitValue = true
            task.inputs.file "${projectDir}/src/main/c/suer.c"
            task.outputs.file "${buildDir}/suer$arch"
            task.commandLine 'gcc'
            task.args "-m$arch", '-o', "${buildDir}/suer$arch", "${projectDir}/src/main/c/suer.c", '-lutil', '-lpthread'
        }
        task(suer64, type: Exec) { task -> configureSuer(task, '64') }
        task(suer32, type: Exec) { task -> configureSuer(task, '32') }
        task(suer).dependsOn suer64, suer32
        build.dependsOn suer
    }
}

project(':programming-test') {
    dependencies {
        testCompile(
                'commons-io:commons-io:2.4',
                'org.apache.commons:commons-vfs2:2.0',

                project(':programming-annotation'),
                project(':programming-core'),
                project(':programming-extensions').subprojects,

                files("${rootDir}/lib/node/sigar/sigar.jar")
        )
        testRuntime fileTree(dir: "${rootDir}/lib/node/sigar/", exclude: "*.jar")
        testRuntime 'org.slf4j:slf4j-log4j12:1.6.1'
    }

    def testConfiguration = { Test task ->
        task.with {
            ignoreFailures = project.hasProperty('ignoreTestFailures') ? project.property('ignoreTestFailures').toBoolean() : false
            forkEvery 1
            testLogging {
                exceptionFormat = 'full'
            }
            beforeTest { descriptor ->
                logger.lifecycle("Running: " + descriptor)
            }

            systemProperties << ['proactive.home': rootDir.absolutePath]
            systemProperties << ['proactive.runtime.ping': false]
            systemProperties << ['java.security.policy': file('src/test/resources/proactive.java.policy').absolutePath]
            dependsOn 'copyToLib'
            dependsOn '::ProActiveJar'
        }
    }

    task functionalTest(type: Test, group: 'ProActive')
    functionalTest.configure testConfiguration
    functionalTest.configure {
        include 'functionalTests/**'
        include 'gcmdeployment/**'
        exclude '**/TestHeartbeat*'
    }

    def protocols = ['rmi', 'pnp', 'pamr', 'pnps', 'rmissl', 'rmissh', 'http']
    protocols.each { protocol ->
        def testProtocol = task "functionalTest-$protocol"(type: Test)
        testProtocol.configure testConfiguration
        testProtocol.configure {
            include 'functionalTests/**'
            exclude '**/TestHeartbeat*'
            reports.junitXml.destination file("${reports.junitXml.destination}/$protocol")
            reports.html.destination file("${reports.html.destination}/$protocol")
            systemProperties << ['proactive.communication.protocol': protocol]
            systemProperties << ['proactive.pamr.router.address': 'localhost']
            systemProperties << ['proactive.pamr.router.port': 0]
        }
    }
    task "functionalTest-all"(dependsOn: protocols.collect { "functionalTest-$it" })

    task pamrHeartbeatTest(type: Test, group: 'ProActive') {
        include '**/TestHeartbeat*'

        systemProperties << ['-Djdwp.port': 5550]
        jvmArgs '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5550'
    }

    task performanceTest(type: Test, group: 'ProActive')
    performanceTest.configure testConfiguration
    performanceTest.configure {
        include 'performanceTests/**'
    }

    // disable test, use allTest to run the tests
    task(allTest).dependsOn pamrHeartbeatTest, functionalTest, performanceTest
    test.enabled = false

    // for the test nodes to have the ProActive jars
    task copyToLib(type: Copy) {
        from configurations.testRuntime
        into "$buildDir/dist/lib"
    }
}

project(':doc').subprojects {
    apply plugin: 'documentation'
    documentation.outputName = project.name
    documentation.docDir = 'src'
    documentation.sharedDir = '../shared'
    documentation.snippetsStart = rootProject.projectDir
}

apply from: 'dist.gradle'
